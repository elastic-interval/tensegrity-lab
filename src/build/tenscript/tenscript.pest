collection =
    { "(" ~ "collection" ~ fabric_plan* ~ brick_definition* ~ ")" }

fabric_plan =
    { "(" ~ "fabric" ~ name ~ surface? ~ build? ~ shape? ~ ")" }
name =
    { "(" ~ "name" ~ string ~ ")" }
shape =
    { "(" ~ "shape" ~ shape_operation+ ~ ")" }
basic_shape_operation =
    { space | join | vulcanize | remove_shapers | replace_faces | set_viscosity }
shape_operation =
    { basic_shape_operation | countdown_block }
countdown_block = // iteration-count
    { "(" ~ "countdown" ~ integer ~ basic_shape_operation+ ~ ")" }
space = // mark-name, spacing-factor
    { "(" ~ "space" ~ atom ~ float ~ ")" }
join = // mark-name
    { "(" ~ "join" ~ atom ~ ")" }
remove_shapers = // shaper-names
    { "(" ~ "remove-shapers" ~ atom* ~ ")" }
set_viscosity =
    { "(" ~ "set-viscosity" ~ float ~ ")" }
vulcanize =
    { "(" ~ "vulcanize" ~ ")" }
replace_faces =
    { "(" ~ "replace-faces" ~ ")" }
build =
    { "(" ~ "build" ~ seed? ~ build_node? ~ ")" }
build_node =
    { on_face | grow | mark | branch }
on_face =
    { "(" ~ "face" ~ face_name ~ build_node ~ ")" }
grow =
    { "(" ~ "grow" ~ (integer | forward) ~ scale? ~ build_node? ~ ")" }
mark =
    { "(" ~ "mark" ~ atom ~ ")" }
branch =
    { "(" ~ "branch" ~ on_face+ ~ ")" }
scale =
    { "(" ~ "scale" ~ float ~ ")" }
seed =
    { "(" ~ "seed" ~ brick_name ~ orient_down? ~ ")" }
brick_name =
    { ":left-mitosis" | ":right-mitosis" | ":left-omni" | ":right-omni" | ":left" | ":right" }
orient_down =
    { "(" ~ "orient-down" ~ face_name+ ~ ")" }
surface =
    { "(" ~ "surface" ~ surface_character ~ ")" }
surface_character =
    { ":bouncy" | ":frozen" | ":sticky" }

brick_definition =
    { "(" ~ "brick" ~ name ~ brick_proto ~ brick_baked? ~ ")" }
brick_proto =
    { "(" ~ "proto" ~ pushes_proto+ ~ pulls_proto+ ~ faces_proto+ ~ ")" }
pushes_proto = // axis, ideal
    { "(" ~ "pushes" ~ axis ~ float ~ push_proto+ ~ ")" }
axis = { axis_x | axis_y | axis_z }
axis_x = { ":x-axis" | "X" }
axis_y = { ":y-axis" | "Y" }
axis_z = { ":z-axis" | "Z" }
push_proto = // alpha, omega
    { "(" ~ "push" ~ atom ~ atom ~ ")" }
pulls_proto = // ideal
    { "(" ~ "pulls" ~ float ~ pull_proto+ ~ ")" }
pull_proto = // alpha, omega
    { "(" ~ "pull" ~ atom ~ atom ~ ")" }
faces_proto =
    { "(" ~ "faces" ~ face_proto+ ~ ")" }
chirality = { left | right }
left = { "left" }
right = { "right" }
face_proto = // a, b, c, name
    { "(" ~ chirality ~ atom ~ atom ~ atom ~ atom ~ ")" }
brick_baked =
    { "(" ~ "baked" ~ joint_baked+ ~ interval_baked+ ~ face_baked+ ~ ")" }
joint_baked = // x, y, z
    { "(" ~ "joint" ~ float ~ float ~ float ~ ")" }
interval_baked = // alpha_index, omega_index, strain
    { "(" ~ interval_role ~ integer ~ integer ~ float ~ ")" }
interval_role = { push | pull }
push = { "push" }
pull = { "pull" }
face_baked = // a, b, c, name
    { "(" ~ chirality ~ integer ~ integer ~ integer ~ atom ~ ")" }

face_name = @{"F" ~ integer}
ident = @{ XID_START ~ (XID_CONTINUE | "-")* }
ident_uppercase = @{ ASCII_ALPHA_UPPER ~ (XID_CONTINUE | "-")* }
atom = @{ (":" ~ ident) | ident_uppercase }
float = @{ (integer? ~ "." ~ mantissa) | integer }
integer = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
forward = @{ "\"" ~  ("X" | ".")+ ~ "\"" }

WHITESPACE = _{ " " | "\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
