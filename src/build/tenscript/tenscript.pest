fabric_library =
    { "(" ~ "fabric-library" ~ fabric_plan* ~ ")" }

fabric_plan =
    { "(" ~ "fabric" ~ name ~ build ~ shape ~ pretense ~ converge? ~ animate? ~ scale? ~ ")" }
name =
    { "(" ~ "name" ~ string+ ~ ")" }
shape =
    { "(" ~ "shape" ~ shape_operation* ~ ")" }
basic_shape_operation =
    {
    anchor | guy_line | spacer | joiner | vulcanize |
    set_rigidity | set_drag | set_viscosity | down | centralize | omit | add
    }
shape_operation =
    { basic_shape_operation | during }
during = // iteration-count
    { "(" ~ "during" ~ float ~ basic_shape_operation+ ~ ")" }
omit =
    {  "(" ~ "omit" ~ integer ~ integer ~ ")" }
add =
    {  "(" ~ "add" ~ integer ~ integer ~ float? ~ ")" }
anchor = // joint number, location
    { "(" ~ "anchor" ~ integer ~ surface_location ~ ")" }
guy_line = // joint number, length, location
    { "(" ~ "guy-line" ~ integer ~ float ~ surface_location ~ ")" }
surface_location =
    { "(" ~ "surface" ~ float ~ float ~ ")" }
spacer = // mark-name, spacing-factor
    { "(" ~ "space" ~ atom ~ float ~ ")" }
joiner = // mark-name
    { "(" ~ "join" ~ atom ~ seed? ~ ")" }
down = // mark-name
    { "(" ~ "down" ~ atom ~ ")" }
set_rigidity =
    { "(" ~ "set-rigidity" ~ float ~ ")" }
set_drag =
    { "(" ~ "set-drag" ~ float ~ ")" }
centralize =
    { "(" ~ "centralize" ~ float? ~ ")" }
set_viscosity =
    { "(" ~ "set-viscosity" ~ float ~ ")" }
vulcanize =
    { "(" ~ "vulcanize" ~ ")" }
pretense =
    { "(" ~ "pretense" ~ pretense_feature* ~ ")" }
pretense_feature =
    { surface | pretenst | seconds | rigidity | altitude | viscosity | drag }
surface =
    { "(" ~ "surface" ~ surface_character ~ ")" }
surface_character =
    { ":frozen" | ":bouncy" | ":absent" | ":sticky" | ":slippery" }
converge =
    { "(" ~ "converge" ~ float ~ ")" }
animate =
    { "(" ~ "animate" ~ frequency ~ animate_feature* ~ ")" }
animate_feature =
    { contraction | muscle_interval }
contraction =
    { "(" ~ "contraction" ~ float ~ ")" }
frequency =
    { "(" ~ "frequency" ~ float ~ ")" }
muscle_interval =
    { "(" ~ "muscle" ~ integer ~ integer ~ muscle_group ~ ")" }
muscle_group =
    { ":alpha" | ":omega" }
pretenst =
    { "(" ~ "pretenst" ~ float ~ ")" }
rigidity =
    { "(" ~ "rigidity" ~ float ~ ")" }
altitude =
    { "(" ~ "altitude" ~ float ~ ")" }
seconds =
    { "(" ~ "seconds" ~ float ~ ")" }
viscosity =
    { "(" ~ "viscosity" ~ float ~ ")" }
drag =
    { "(" ~ "drag" ~ float ~ ")" }
build =
    { "(" ~ "build" ~ branch ~ ")" }
build_node =
    { grow | mark | branch | prism }
on_face =
    { "(" ~ "on-face" ~ face_alias ~ build_node ~ ")" }
grow =
    { "(" ~ "grow" ~ (integer | forward) ~ scale? ~ build_node* ~ ")" }
mark =
    { "(" ~ "mark" ~ atom ~ ")" }
seed =
    { "(" ~ "seed" ~ integer ~")"}
branch =
    { "(" ~ "branch" ~ partial_alias ~ scale? ~ seed? ~ face_rotation* ~ on_face* ~ ")" }
prism =
    { "(" ~ "prism" ~ ")" }
face_rotation =
    { "(" ~ "rotate" ~ ")" }
scale =
    { "(" ~ "scale" ~ float ~ ")" }

// Shared alias rules used by fabric plans
partial_alias =
    { "(" ~ "alias" ~ atom ~ ")" }
face_alias =
    { "(" ~ "alias" ~ atom+ ~ ")" }

ident = @{ XID_START ~ (XID_CONTINUE | "-")* }
ident_uppercase = @{ ASCII_ALPHA_UPPER ~ (XID_CONTINUE | "-")* }
atom = @{ (":" ~ ident) | (ident_uppercase ~ (":" ~ ident_uppercase)*) }
float = @{ "-"? ~ (integer? ~ "." ~ mantissa) | integer }
integer = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
forward = @{ "\"" ~  ("X" | ".")+ ~ "\"" }

WHITESPACE = _{ " " | "\n"| "\t" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
