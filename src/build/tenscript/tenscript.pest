brick_library =
    { "(" ~ "brick-library" ~ brick_definition* ~ ")" }
fabric_library =
    { "(" ~ "fabric-library" ~ fabric_plan* ~ ")" }

fabric_plan =
    { "(" ~ "fabric" ~ name ~ build ~ shape? ~ pretense? ~ ")" }
name =
    { "(" ~ "name" ~ string+ ~ ")" }
shape =
    { "(" ~ "shape" ~ shape_operation* ~ ")" }
basic_shape_operation =
    { spacer | joiner | vulcanize | remove_spacers | faces_to_triangles | set_viscosity | down }
shape_operation =
    { basic_shape_operation | during_count }
during_count = // iteration-count
    { "(" ~ "during" ~ integer ~ basic_shape_operation+ ~ ")" }
spacer = // mark-name, spacing-factor
    { "(" ~ "space" ~ atom ~ float ~ ")" }
joiner = // mark-name
    { "(" ~ "join" ~ atom ~ seed? ~ ")" }
down = // mark-name
    { "(" ~ "down" ~ atom ~ ")" }
remove_spacers = // spacer-names
    { "(" ~ "remove-spacers" ~ atom* ~ ")" }
set_viscosity =
    { "(" ~ "set-viscosity" ~ float ~ ")" }
vulcanize =
    { "(" ~ "vulcanize" ~ ")" }
pretense =
    { "(" ~ "pretense" ~ pretense_feature* ~ ")" }
pretense_feature =
    { surface | muscle | pretense_factor }
surface =
    { "(" ~ "surface" ~ surface_character ~ ")" }
surface_character =
    { ":frozen" | ":bouncy" | ":absent" | ":sticky" }
muscle =
    { "(" ~ "muscle" ~ float ~ integer ~ integer* ~ ")" }
pretense_factor =
    { "(" ~ "pretense-factor" ~ float ~ ")" }
faces_to_triangles =
    { "(" ~ "faces-to-triangles" ~ ")" }
build =
    { "(" ~ "build" ~ branch ~ ")" }
build_node =
    { on_face | grow | mark | branch }
on_face =
    { "(" ~ "face" ~ face_alias ~ build_node ~ ")" }
grow =
    { "(" ~ "grow" ~ (integer | forward) ~ scale? ~ build_node? ~ ")" }
mark =
    { "(" ~ "mark" ~ atom ~ ")" }
seed =
    { "(" ~ "seed" ~ integer ~")"}
group =
    { "(" ~ "group" ~ integer ~")"}
branch =
    { "(" ~ "branch" ~ partial_alias ~ group? ~ scale? ~ seed? ~ face_rotation* ~ on_face* ~ ")" }
face_rotation =
    { "(" ~ "rotate" ~ ")" }
scale =
    { "(" ~ "scale" ~ float ~ ")" }

brick_definition =
    { "(" ~ "brick" ~ brick_proto ~ brick_baked? ~ ")" }
partial_alias =
    { "(" ~ "alias" ~ atom ~ ")" }
brick_proto =
    { "(" ~ "proto" ~ partial_alias ~ joints_proto? ~ pushes_proto+ ~ pulls_proto* ~ faces_proto+ ~ face_aliases+ ~ ")" }
pushes_proto = // axis, ideal
    { "(" ~ "pushes" ~ axis ~ float ~ push_proto+ ~ ")" }
axis = { axis_x | axis_y | axis_z }
axis_x = { ":x-axis" | "X" }
axis_y = { ":y-axis" | "Y" }
axis_z = { ":z-axis" | "Z" }
joints_proto = // alpha, omega
    { "(" ~ "joints" ~ atom+ ~ ")" }
push_proto = // alpha, omega
    { "(" ~ "push" ~ atom ~ atom ~ ")" }
pulls_proto = // ideal
    { "(" ~ "pulls" ~ float ~ pull_proto+ ~ ")" }
pull_proto = // alpha, omega, material-name
    { "(" ~ "pull" ~ atom ~ atom ~ atom ~ ")" }
faces_proto =
    { "(" ~ "faces" ~ face_proto+ ~ ")" }
left = { "left" }
right = { "right" }
chirality = { left | right }
face_proto = // a, b, c, name
    { "(" ~ chirality ~ atom ~ atom ~ atom ~ face_alias* ~ ")" }
face_alias =
    { "(" ~ "alias" ~ atom+ ~ ")" }
face_aliases =
    { "(" ~ "face-aliases" ~ face_aliases_with ~ face_alias+ ~ ")" }
face_aliases_with =
    { "(" ~ "with" ~ atom+ ~ ")" }
brick_baked =
    { "(" ~ "baked" ~ joint_baked+ ~ interval_baked+ ~ face_baked+ ~ ")" }
joint_baked = // x, y, z
    { "(" ~ "joint" ~ float ~ float ~ float ~ ")" }
interval_baked = // alpha_index, omega_index, strain, material-name
    { "(" ~ "interval" ~ integer ~ integer ~ float ~ atom ~ ")" }
face_baked = // a, b, c, name
    { "(" ~ chirality ~ integer ~ integer ~ integer ~ face_alias* ~ ")" }

ident = @{ XID_START ~ (XID_CONTINUE | "-")* }
ident_uppercase = @{ ASCII_ALPHA_UPPER ~ (XID_CONTINUE | "-")* }
atom = @{ (":" ~ ident) | (ident_uppercase ~ (":" ~ ident_uppercase)*) }
float = @{ "-"? ~ (integer? ~ "." ~ mantissa) | integer }
integer = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
forward = @{ "\"" ~  ("X" | ".")+ ~ "\"" }

WHITESPACE = _{ " " | "\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
