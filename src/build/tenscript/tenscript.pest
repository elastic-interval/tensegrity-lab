// Created by intellij-pest on 2023-01-25
// tenscript
// Author: mitch
//

fabric_plan = {
    "(" ~ "fabric" ~
    surface? ~
    build? ~
    shape? ~
    ")"
}

shape = {
    "(" ~ "shape" ~
        shape_operation+ ~
    ")"
}

basic_shape_operation = {
    space |
    join |
    vulcanize |
    remove_shapers |
    replace_faces
}

shape_operation = {
    basic_shape_operation |
    countdown_block
}

countdown_block = {
    "(" ~ "countdown" ~ integer ~
        basic_shape_operation+ ~
    ")"
}

space = {
    "(" ~ "space" ~ atom ~ number ~ ")"
}

join = {
    "(" ~ "join" ~ atom ~ ")"
}

remove_shapers = {
    "(" ~ "remove-shapers" ~ atom* ~ ")"
}

vulcanize = {
    "(" ~ "vulcanize" ~ ")"
}

replace_faces = {
    "(" ~ "replace-faces" ~ ")"
}

build = {
    "(" ~ "build" ~
    seed? ~
    build_node? ~
    ")"
}

build_node = {
    face | grow | mark | branch
}

face = {
    "(" ~ "face" ~
    face_name ~
    build_node ~
    ")"
}

face_name = {
    "A-" | "B-" |
    "C-" | "D-" |
    "A+" | "B+" |
    "C+" | "D+"
}

grow = {
    "(" ~ "grow" ~
    (integer | forward) ~
    scale? ~
    build_node? ~
    ")"
}

mark = {
    "(" ~ "mark" ~ atom ~ ")"
}

branch = {
    "(" ~ "branch" ~
    build_node+ ~
    ")"
}

scale = {
    "(" ~
    "scale" ~
    number ~
    ")"
}

seed = {
    "(" ~
    "seed" ~
    seed_value ~
    orient_down? ~
    ")"
}

seed_value = {
    ":left-right" |
    ":right-left" |
    ":left" |
    ":right"
}

orient_down = {
    "(" ~
    "orient-down" ~
    face_name+ ~
    ")"
}

surface = {
    "(" ~
    "surface" ~
    surface_character ~
    ")"
}

surface_character = {
    ":bouncy" |
    ":frozen" |
    ":sticky"
}

ident = @{
    XID_START ~ (XID_CONTINUE | "-")*
}

atom = @{ ":" ~ ident }

number = @{
    integer? ~
    "." ~
    mantissa
}
integer = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = { ASCII_DIGIT+ }

string = @{
    "\"" ~ (!"\"" ~ ANY)* ~ "\""
}

forward = @{
    "\"" ~  ("X" | ".")+ ~ "\""
}

WHITESPACE = _{ " " | "\n" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
