brick_library =
    { "(" ~ "brick-library" ~ brick_definition* ~ ")" }
fabric_library =
    { "(" ~ "fabric-library" ~ fabric_plan* ~ ")" }

fabric_plan =
    { "(" ~ "fabric" ~ name ~ build ~ shape ~ pretense ~ scale? ~ ")" }
name =
    { "(" ~ "name" ~ string+ ~ ")" }
shape =
    { "(" ~ "shape" ~ shape_operation* ~ ")" }
basic_shape_operation =
    {
    anchor | guy_line | spacer | joiner | vulcanize |
    set_rigidity | set_drag | set_viscosity | down | centralize | omit | add
    }
shape_operation =
    { basic_shape_operation | during }
during = // iteration-count
    { "(" ~ "during" ~ float ~ basic_shape_operation+ ~ ")" }
omit =
    {  "(" ~ "omit" ~ integer ~ integer ~ ")" }
add =
    {  "(" ~ "add" ~ integer ~ integer ~ float? ~ ")" }
anchor = // joint number, location
    { "(" ~ "anchor" ~ integer ~ surface_location ~ ")" }
guy_line = // joint number, length, location
    { "(" ~ "guy-line" ~ integer ~ float ~ surface_location ~ atom? ~ ")" }
surface_location =
    { "(" ~ "surface" ~ float ~ float ~ ")" }
spacer = // mark-name, spacing-factor
    { "(" ~ "space" ~ atom ~ float ~ ")" }
joiner = // mark-name
    { "(" ~ "join" ~ atom ~ seed? ~ ")" }
down = // mark-name
    { "(" ~ "down" ~ atom ~ ")" }
set_rigidity =
    { "(" ~ "set-rigidity" ~ float ~ ")" }
set_drag =
    { "(" ~ "set-drag" ~ float ~ ")" }
centralize =
    { "(" ~ "centralize" ~ float? ~ ")" }
set_viscosity =
    { "(" ~ "set-viscosity" ~ float ~ ")" }
vulcanize =
    { "(" ~ "vulcanize" ~ ")" }
pretense =
    { "(" ~ "pretense" ~ pretense_feature* ~ ")" }
pretense_feature =
    { surface | muscle | pretenst | seconds | rigidity | altitude | viscosity | drag }
surface =
    { "(" ~ "surface" ~ surface_character ~ ")" }
surface_character =
    { ":frozen" | ":bouncy" | ":absent" | ":sticky" }
muscle =
    { "(" ~ "muscle" ~ float ~ integer ~ integer* ~ ")" }
pretenst =
    { "(" ~ "pretenst" ~ float ~ ")" }
rigidity =
    { "(" ~ "rigidity" ~ float ~ ")" }
altitude =
    { "(" ~ "altitude" ~ float ~ ")" }
seconds =
    { "(" ~ "seconds" ~ float ~ ")" }
viscosity =
    { "(" ~ "viscosity" ~ float ~ ")" }
drag =
    { "(" ~ "drag" ~ float ~ ")" }
build =
    { "(" ~ "build" ~ branch ~ ")" }
build_node =
    { grow | mark | branch | prism }
on_face =
    { "(" ~ "on-face" ~ face_alias ~ build_node ~ ")" }
grow =
    { "(" ~ "grow" ~ (integer | forward) ~ scale? ~ build_node* ~ ")" }
mark =
    { "(" ~ "mark" ~ atom ~ ")" }
seed =
    { "(" ~ "seed" ~ integer ~")"}
branch =
    { "(" ~ "branch" ~ partial_alias ~ scale? ~ seed? ~ face_rotation* ~ on_face* ~ ")" }
prism =
    { "(" ~ "prism" ~ ")" }
face_rotation =
    { "(" ~ "rotate" ~ ")" }
scale =
    { "(" ~ "scale" ~ float ~ ")" }

brick_definition =
    { "(" ~ "brick" ~ brick_proto ~ brick_baked? ~ ")" }
partial_alias =
    { "(" ~ "alias" ~ atom ~ ")" }
brick_proto =
    { "(" ~ "proto" ~ partial_alias ~ joints_proto? ~ pushes_proto+ ~ pulls_proto* ~ faces_proto+ ~ face_aliases+ ~ ")" }
pushes_proto = // axis, ideal
    { "(" ~ "pushes" ~ axis ~ float ~ push_proto+ ~ ")" }
axis = { axis_x | axis_y | axis_z }
axis_x = { ":x-axis" | "X" }
axis_y = { ":y-axis" | "Y" }
axis_z = { ":z-axis" | "Z" }
joints_proto = // alpha, omega
    { "(" ~ "joints" ~ atom+ ~ ")" }
push_proto = // alpha, omega
    { "(" ~ "push" ~ atom ~ atom ~ ")" }
pulls_proto = // ideal
    { "(" ~ "pulls" ~ float ~ pull_proto+ ~ ")" }
pull_proto = // alpha, omega, material-name
    { "(" ~ "pull" ~ atom ~ atom ~ atom ~ ")" }
faces_proto =
    { "(" ~ "faces" ~ face_proto+ ~ ")" }
left = { "left" }
right = { "right" }
chirality = { left | right }
face_proto = // a, b, c, name
    { "(" ~ chirality ~ atom ~ atom ~ atom ~ face_alias* ~ ")" }
face_alias =
    { "(" ~ "alias" ~ atom+ ~ ")" }
face_aliases =
    { "(" ~ "face-aliases" ~ face_aliases_with ~ face_alias+ ~ ")" }
face_aliases_with =
    { "(" ~ "with" ~ atom+ ~ ")" }
brick_baked =
    { "(" ~ "baked" ~ joint_baked+ ~ interval_baked+ ~ face_baked+ ~ ")" }
joint_baked = // x, y, z
    { "(" ~ "joint" ~ float ~ float ~ float ~ ")" }
interval_baked = // alpha_index, omega_index, strain, material-name
    { "(" ~ "interval" ~ integer ~ integer ~ float ~ atom ~ ")" }
face_baked = // a, b, c, name
    { "(" ~ chirality ~ integer ~ integer ~ integer ~ face_alias* ~ ")" }

ident = @{ XID_START ~ (XID_CONTINUE | "-")* }
ident_uppercase = @{ ASCII_ALPHA_UPPER ~ (XID_CONTINUE | "-")* }
atom = @{ (":" ~ ident) | (ident_uppercase ~ (":" ~ ident_uppercase)*) }
float = @{ "-"? ~ (integer? ~ "." ~ mantissa) | integer }
integer = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
forward = @{ "\"" ~  ("X" | ".")+ ~ "\"" }

WHITESPACE = _{ " " | "\n"| "\t" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
